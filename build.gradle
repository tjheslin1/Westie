group 'io.github.tjheslin1'
version '1.1.3'

apply plugin: 'java'

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'org.json', name: 'json', version: '20160810'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "com.github.tomakehurst:wiremock:2.3.1"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def sonatypeUsername = hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv('sonatypeUsername')
def sonatypePassword = hasProperty('sonatypePassword') ? sonatypePassword : System.getenv('sonatypePassword')

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Westie'
                packaging 'jar'
                description 'Project-specific static analysis with common, pre-written examples'
                url 'https://github.com/tjheslin1/Westie'

                scm {
                    url 'scm:git@github.com:Westie/Westie.git'
                    connection 'scm:git@github.com:Westie/Westie.git'
                    developerConnection 'scm:git@github.com:Westie/Westie.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tjheslin1'
                        name 'Thomas Heslin'
                    }
                }
            }
        }
    }
}